<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>SMT.Backend.CVC4</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--allow-exec</a> <a id="26" class="Symbol">#-}</a>

<a id="31" class="Keyword">open</a> <a id="36" class="Keyword">import</a> <a id="43" href="SMT.Theory.html" class="Module">SMT.Theory</a>
<a id="54" class="Keyword">open</a> <a id="59" class="Keyword">import</a> <a id="66" href="SMT.Theory.Reflection.html" class="Module">SMT.Theory.Reflection</a>

<a id="89" class="Keyword">module</a> <a id="96" href="SMT.Backend.CVC4.html" class="Module">SMT.Backend.CVC4</a> <a id="113" class="Symbol">{</a><a id="114" href="SMT.Backend.CVC4.html#114" class="Bound">theory</a> <a id="121" class="Symbol">:</a> <a id="123" href="SMT.Theory.html#1820" class="Record">Theory</a><a id="129" class="Symbol">}</a> <a id="131" class="Symbol">(</a><a id="132" href="SMT.Backend.CVC4.html#132" class="Bound">reflectable</a> <a id="144" class="Symbol">:</a> <a id="146" href="SMT.Theory.Reflection.html#338" class="Record">Reflectable</a> <a id="158" href="SMT.Backend.CVC4.html#114" class="Bound">theory</a><a id="164" class="Symbol">)</a> <a id="166" class="Keyword">where</a>

<a id="173" class="Keyword">open</a> <a id="178" class="Keyword">import</a> <a id="185" href="Data.List.html" class="Module">Data.List</a> <a id="195" class="Symbol">as</a> <a id="198" class="Module">List</a> <a id="203" class="Keyword">using</a> <a id="209" class="Symbol">(</a><a id="210" href="Agda.Builtin.List.html#148" class="Datatype">List</a><a id="214" class="Symbol">;</a> <a id="216" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">_∷_</a><a id="219" class="Symbol">;</a> <a id="221" href="Data.List.Base.html#9981" class="InductiveConstructor">[]</a><a id="223" class="Symbol">)</a>
<a id="225" class="Keyword">open</a> <a id="230" class="Keyword">import</a> <a id="237" href="Data.Maybe.html" class="Module">Data.Maybe</a> <a id="248" class="Symbol">as</a> <a id="251" class="Module">Maybe</a> <a id="257" class="Keyword">using</a> <a id="263" class="Symbol">(</a><a id="264" href="Agda.Builtin.Maybe.html#136" class="Datatype">Maybe</a><a id="269" class="Symbol">;</a> <a id="271" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor">just</a><a id="275" class="Symbol">;</a> <a id="277" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">nothing</a><a id="284" class="Symbol">)</a>
<a id="286" class="Keyword">open</a> <a id="291" class="Keyword">import</a> <a id="298" href="Data.Product.html" class="Module">Data.Product</a> <a id="311" class="Keyword">using</a> <a id="317" class="Symbol">(</a><a id="318" href="Data.Product.html#1167" class="Function Operator">_×_</a><a id="321" class="Symbol">;</a> <a id="323" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">_,_</a><a id="326" class="Symbol">;</a> <a id="328" href="Agda.Builtin.Sigma.html#252" class="Field">proj₁</a><a id="333" class="Symbol">;</a> <a id="335" href="Agda.Builtin.Sigma.html#264" class="Field">proj₂</a><a id="340" class="Symbol">)</a>
<a id="342" class="Keyword">open</a> <a id="347" class="Keyword">import</a> <a id="354" href="Data.String.html" class="Module">Data.String</a> <a id="366" class="Symbol">as</a> <a id="369" class="Module">String</a> <a id="376" class="Keyword">using</a> <a id="382" class="Symbol">(</a><a id="383" href="Agda.Builtin.String.html#336" class="Postulate">String</a><a id="389" class="Symbol">;</a> <a id="391" href="Data.String.Base.html#2177" class="Function Operator">_++_</a><a id="395" class="Symbol">)</a>
<a id="397" class="Keyword">open</a> <a id="402" class="Keyword">import</a> <a id="409" href="Data.Sum.html" class="Module">Data.Sum</a> <a id="418" class="Symbol">as</a> <a id="421" class="Module">Sum</a> <a id="425" class="Keyword">using</a> <a id="431" class="Symbol">(</a><a id="432" href="Data.Sum.Base.html#728" class="Datatype Operator">_⊎_</a><a id="435" class="Symbol">;</a> <a id="437" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a><a id="441" class="Symbol">;</a> <a id="443" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a><a id="447" class="Symbol">;</a> <a id="449" href="Data.Sum.Base.html#914" class="Function Operator">[_,_]</a><a id="454" class="Symbol">)</a>
<a id="456" class="Keyword">open</a> <a id="461" class="Keyword">import</a> <a id="468" href="Data.Unit.html" class="Module">Data.Unit</a> <a id="478" class="Symbol">as</a> <a id="481" class="Module">Unit</a> <a id="486" class="Keyword">using</a> <a id="492" class="Symbol">(</a><a id="493" href="Agda.Builtin.Unit.html#164" class="Record">⊤</a><a id="494" class="Symbol">)</a>
<a id="496" class="Keyword">open</a> <a id="501" class="Keyword">import</a> <a id="508" href="Function.html" class="Module">Function</a> <a id="517" class="Keyword">using</a> <a id="523" class="Symbol">(</a><a id="524" href="Function.Base.html#4121" class="Function Operator">case_of_</a><a id="532" class="Symbol">;</a> <a id="534" href="Function.Base.html#636" class="Function">const</a><a id="539" class="Symbol">;</a> <a id="541" href="Function.Base.html#1919" class="Function Operator">_$_</a><a id="544" class="Symbol">;</a> <a id="546" href="Function.Base.html#1031" class="Function Operator">_∘_</a><a id="549" class="Symbol">)</a>
<a id="551" class="Keyword">open</a> <a id="556" class="Keyword">import</a> <a id="563" href="Reflection.html" class="Module">Reflection</a> <a id="574" class="Symbol">as</a> <a id="577" class="Module">Rfl</a> <a id="581" class="Keyword">using</a> <a id="587" class="Symbol">(</a><a id="588" href="Agda.Builtin.Reflection.html#7543" class="Postulate">return</a><a id="594" class="Symbol">;</a> <a id="596" href="Reflection.TypeChecking.Monad.Syntax.html#682" class="Function Operator">_&gt;&gt;=_</a><a id="601" class="Symbol">;</a> <a id="603" href="Reflection.TypeChecking.Monad.Syntax.html#752" class="Function Operator">_&gt;&gt;_</a><a id="607" class="Symbol">)</a>
<a id="609" class="Keyword">open</a> <a id="614" class="Keyword">import</a> <a id="621" href="Reflection.External.html" class="Module">Reflection.External</a>
<a id="641" class="Keyword">open</a> <a id="646" class="Keyword">import</a> <a id="653" href="SMT.Script.html" class="Module">SMT.Script</a> <a id="664" href="SMT.Backend.CVC4.html#132" class="Bound">reflectable</a> <a id="676" class="Keyword">public</a>
<a id="683" class="Keyword">open</a> <a id="688" class="Keyword">import</a> <a id="695" href="Text.Parser.String.html" class="Module">Text.Parser.String</a> <a id="714" class="Keyword">using</a> <a id="720" class="Symbol">(</a><a id="721" href="Text.Parser.String.html#2611" class="Function">runParser</a><a id="730" class="Symbol">)</a>
<a id="732" class="Keyword">open</a> <a id="737" class="Keyword">import</a> <a id="744" href="SMT.Backend.Base.html" class="Module">SMT.Backend.Base</a>
<a id="761" class="Keyword">open</a> <a id="766" href="SMT.Theory.html#1820" class="Module">Theory</a> <a id="773" href="SMT.Backend.CVC4.html#114" class="Bound">theory</a>

<a id="781" class="Keyword">private</a>
  <a id="791" class="Keyword">variable</a>
    <a id="804" href="SMT.Backend.CVC4.html#804" class="Generalizable">Γ</a> <a id="806" class="Symbol">:</a> <a id="808" href="SMT.Script.Base.html#1107" class="Function">Ctxt</a>
    <a id="817" href="SMT.Backend.CVC4.html#817" class="Generalizable">ξ</a> <a id="819" class="Symbol">:</a> <a id="821" href="SMT.Script.Base.html#4790" class="Datatype">OutputType</a>
    <a id="836" href="SMT.Backend.CVC4.html#836" class="Generalizable">Ξ</a> <a id="838" class="Symbol">:</a> <a id="840" href="SMT.Script.Base.html#4884" class="Function">OutputCtxt</a>

<a id="cvc4TC"></a><a id="852" href="SMT.Backend.CVC4.html#852" class="Function">cvc4TC</a> <a id="859" class="Symbol">:</a> <a id="861" href="SMT.Script.Base.html#8886" class="Datatype">Script</a> <a id="868" href="Agda.Builtin.List.html#185" class="InductiveConstructor">[]</a> <a id="871" href="SMT.Backend.CVC4.html#804" class="Generalizable">Γ</a> <a id="873" class="Symbol">(</a><a id="874" href="SMT.Backend.CVC4.html#817" class="Generalizable">ξ</a> <a id="876" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="878" href="SMT.Backend.CVC4.html#836" class="Generalizable">Ξ</a><a id="879" class="Symbol">)</a> <a id="881" class="Symbol">→</a> <a id="883" href="Agda.Builtin.Reflection.html#7500" class="Postulate">Rfl.TC</a> <a id="890" class="Symbol">(</a><a id="891" href="SMT.Script.Base.html#6399" class="Datatype">Outputs</a> <a id="899" class="Symbol">(</a><a id="900" href="SMT.Backend.CVC4.html#817" class="Generalizable">ξ</a> <a id="902" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="904" href="SMT.Backend.CVC4.html#836" class="Generalizable">Ξ</a><a id="905" class="Symbol">))</a>
<a id="908" href="SMT.Backend.CVC4.html#852" class="Function">cvc4TC</a> <a id="915" class="Symbol">{</a><a id="916" href="SMT.Backend.CVC4.html#916" class="Bound">Γ</a><a id="917" class="Symbol">}</a> <a id="919" class="Symbol">{</a><a id="920" href="SMT.Backend.CVC4.html#920" class="Bound">ξ</a><a id="921" class="Symbol">}</a> <a id="923" class="Symbol">{</a><a id="924" href="SMT.Backend.CVC4.html#924" class="Bound">Ξ</a><a id="925" class="Symbol">}</a> <a id="927" href="SMT.Backend.CVC4.html#927" class="Bound">scr</a> <a id="931" class="Symbol">=</a> <a id="933" class="Keyword">do</a>

  <a id="939" class="Comment">-- Print the SMT-LIB script and build the output parser.</a>
  <a id="998" class="Keyword">let</a> <a id="1002" class="Symbol">(</a><a id="1003" href="SMT.Backend.CVC4.html#1003" class="Bound">scr</a> <a id="1007" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1009" href="SMT.Backend.CVC4.html#1009" class="Bound">parseOutputs</a><a id="1021" class="Symbol">)</a> <a id="1023" class="Symbol">=</a> <a id="1025" href="SMT.Script.Show.html#9296" class="Function">showScript</a> <a id="1036" href="SMT.Backend.CVC4.html#927" class="Bound">scr</a>

  <a id="1043" class="Comment">-- Construct the command specification.</a>
  <a id="1085" class="Comment">--</a>
  <a id="1090" class="Comment">-- TODO: Inspect the output type to see if any models should be produced,</a>
  <a id="1166" class="Comment">--       and set the --produce-models flag accordingly.</a>
  <a id="1224" class="Comment">--</a>
  <a id="1229" class="Keyword">let</a> <a id="1233" href="SMT.Backend.CVC4.html#1233" class="Bound">cvc4Cmd</a> <a id="1241" class="Symbol">=</a> <a id="1243" class="Keyword">record</a>
                <a id="1266" class="Symbol">{</a> <a id="1268" href="Reflection.External.html#1157" class="Field">name</a>  <a id="1274" class="Symbol">=</a> <a id="1276" class="String">&quot;cvc4&quot;</a>
                <a id="1299" class="Symbol">;</a> <a id="1301" href="Reflection.External.html#1228" class="Field">args</a>  <a id="1307" class="Symbol">=</a> <a id="1309" class="String">&quot;--lang=smt2&quot;</a> <a id="1323" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="1325" class="String">&quot;--output-lang=smt2&quot;</a> <a id="1346" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="1348" class="String">&quot;--produce-models&quot;</a> <a id="1367" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="1369" class="String">&quot;-q&quot;</a> <a id="1374" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="1376" class="String">&quot;-&quot;</a> <a id="1380" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="1382" href="Agda.Builtin.List.html#185" class="InductiveConstructor">[]</a>
                <a id="1401" class="Symbol">;</a> <a id="1403" href="Reflection.External.html#1295" class="Field">input</a> <a id="1409" class="Symbol">=</a> <a id="1411" href="SMT.Backend.CVC4.html#1003" class="Bound">scr</a>
                <a id="1431" class="Symbol">}</a>

  <a id="1436" class="Comment">-- Run the CVC4 command and parse the output.</a>
  <a id="1484" href="Reflection.External.html#1421" class="InductiveConstructor">result</a> <a id="1491" href="SMT.Backend.CVC4.html#1491" class="Bound">exitCode</a> <a id="1500" href="SMT.Backend.CVC4.html#1500" class="Bound">stdout</a> <a id="1507" href="SMT.Backend.CVC4.html#1507" class="Bound">stderr</a> <a id="1514" href="Reflection.TypeChecking.Monad.Syntax.html#682" class="Function Operator">←</a> <a id="1516" href="Reflection.External.html#2504" class="Function">unsafeRunCmdTC</a> <a id="1531" href="SMT.Backend.CVC4.html#1233" class="Bound">cvc4Cmd</a>
  <a id="1541" href="Function.Base.html#4121" class="Function Operator">case</a> <a id="1546" href="Text.Parser.String.html#2611" class="Function">runParser</a> <a id="1556" href="SMT.Backend.CVC4.html#1009" class="Bound">parseOutputs</a> <a id="1569" href="SMT.Backend.CVC4.html#1500" class="Bound">stdout</a> <a id="1576" href="Function.Base.html#4121" class="Function Operator">of</a> <a id="1579" class="Symbol">λ</a> <a id="1581" class="Keyword">where</a>
    <a id="1591" class="Symbol">(</a><a id="1592" href="Data.Sum.Base.html#778" class="InductiveConstructor">inj₁</a> <a id="1597" href="SMT.Backend.CVC4.html#1597" class="Bound">parserr</a><a id="1604" class="Symbol">)</a> <a id="1606" class="Symbol">→</a> <a id="1608" href="SMT.Script.Base.html#10714" class="Function">parseError</a> <a id="1619" href="SMT.Backend.CVC4.html#1500" class="Bound">stdout</a> <a id="1626" href="SMT.Backend.CVC4.html#1597" class="Bound">parserr</a>
    <a id="1638" class="Symbol">(</a><a id="1639" href="Data.Sum.Base.html#803" class="InductiveConstructor">inj₂</a> <a id="1644" href="SMT.Backend.CVC4.html#1644" class="Bound">outputs</a><a id="1651" class="Symbol">)</a> <a id="1653" class="Symbol">→</a> <a id="1655" href="Agda.Builtin.Reflection.html#7543" class="Postulate">return</a> <a id="1662" href="SMT.Backend.CVC4.html#1644" class="Bound">outputs</a>

<a id="1671" class="Keyword">macro</a>
  <a id="cvc4"></a><a id="1679" href="SMT.Backend.CVC4.html#1679" class="Function">cvc4</a> <a id="1684" class="Symbol">:</a> <a id="1686" href="SMT.Script.Base.html#8886" class="Datatype">Script</a> <a id="1693" href="Agda.Builtin.List.html#185" class="InductiveConstructor">[]</a> <a id="1696" href="SMT.Backend.CVC4.html#804" class="Generalizable">Γ</a> <a id="1698" class="Symbol">(</a><a id="1699" href="SMT.Backend.CVC4.html#817" class="Generalizable">ξ</a> <a id="1701" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="1703" href="SMT.Backend.CVC4.html#836" class="Generalizable">Ξ</a><a id="1704" class="Symbol">)</a> <a id="1706" class="Symbol">→</a> <a id="1708" href="Agda.Builtin.Reflection.html#4246" class="Datatype">Rfl.Term</a> <a id="1717" class="Symbol">→</a> <a id="1719" href="Agda.Builtin.Reflection.html#7500" class="Postulate">Rfl.TC</a> <a id="1726" href="Agda.Builtin.Unit.html#164" class="Record">⊤</a>
  <a id="1730" href="SMT.Backend.CVC4.html#1679" class="Function">cvc4</a> <a id="1735" href="SMT.Backend.CVC4.html#1735" class="Bound">scr</a> <a id="1739" href="SMT.Backend.CVC4.html#1739" class="Bound">hole</a> <a id="1744" class="Symbol">=</a> <a id="1746" href="SMT.Backend.CVC4.html#852" class="Function">cvc4TC</a> <a id="1753" href="SMT.Backend.CVC4.html#1735" class="Bound">scr</a> <a id="1757" href="Reflection.TypeChecking.Monad.Syntax.html#682" class="Function Operator">&gt;&gt;=</a> <a id="1761" href="Agda.Builtin.Reflection.html#7673" class="Postulate">Rfl.unify</a> <a id="1771" href="SMT.Backend.CVC4.html#1739" class="Bound">hole</a> <a id="1776" href="Function.Base.html#1031" class="Function Operator">∘</a> <a id="1778" href="SMT.Script.Base.html#7747" class="Function">quoteOutputs</a>

<a id="1792" class="Keyword">macro</a>
  <a id="solveCVC4"></a><a id="1800" href="SMT.Backend.CVC4.html#1800" class="Function">solveCVC4</a> <a id="1810" class="Symbol">:</a> <a id="1812" href="Agda.Builtin.Reflection.html#4246" class="Datatype">Rfl.Term</a> <a id="1821" class="Symbol">→</a> <a id="1823" href="Agda.Builtin.Reflection.html#7500" class="Postulate">Rfl.TC</a> <a id="1830" href="Agda.Builtin.Unit.html#164" class="Record">⊤</a>
  <a id="1834" href="SMT.Backend.CVC4.html#1800" class="Function">solveCVC4</a> <a id="1844" class="Symbol">=</a> <a id="1846" href="SMT.Backend.Base.html#2350" class="Function">solve</a> <a id="1852" class="String">&quot;cvc4&quot;</a> <a id="1859" href="SMT.Backend.CVC4.html#852" class="Function">cvc4TC</a>
    <a id="1870" class="Keyword">where</a>
      <a id="1882" class="Keyword">open</a> <a id="1887" href="SMT.Backend.Base.html#685" class="Module">Solver</a> <a id="1894" href="SMT.Backend.CVC4.html#132" class="Bound">reflectable</a> <a id="1906" class="Keyword">using</a> <a id="1912" class="Symbol">(</a><a id="1913" href="SMT.Backend.Base.html#2350" class="Function">solve</a><a id="1918" class="Symbol">)</a>

</pre></body></html>